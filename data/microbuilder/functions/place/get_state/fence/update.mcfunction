scoreboard players set #place.update.connect microbuilder.calc 0
execute if data storage microbuilder:calc update_neighbours.perspective.neighbour.property{type:"fence"} unless data storage microbuilder:calc update_neighbours.perspective.neighbour.block{id:"minecraft:nether_brick_fence"} unless data storage microbuilder:calc update_neighbours.perspective.this.block{id:"minecraft:nether_brick_fence"} run scoreboard players set #place.update.connect microbuilder.calc 1
execute if data storage microbuilder:calc update_neighbours.perspective{neighbour:{block:{id:"minecraft:nether_brick_fence"}},this:{block:{id:"minecraft:nether_brick_fence"}}} run scoreboard players set #place.update.connect microbuilder.calc 1
execute if data storage microbuilder:calc update_neighbours.perspective.neighbour.state{full:1b} run scoreboard players set #place.update.connect microbuilder.calc 1

execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{east:"false"} if data storage microbuilder:calc update_neighbours.perspective{direction:"east"} if score #place.update.connect microbuilder.calc matches 1 run scoreboard players remove #place.updated_state microbuilder.calc 8
execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{north:"false"} if data storage microbuilder:calc update_neighbours.perspective{direction:"north"} if score #place.update.connect microbuilder.calc matches 1 run scoreboard players remove #place.updated_state microbuilder.calc 4
execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{south:"false"} if data storage microbuilder:calc update_neighbours.perspective{direction:"south"} if score #place.update.connect microbuilder.calc matches 1 run scoreboard players remove #place.updated_state microbuilder.calc 2
execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{west:"false"} if data storage microbuilder:calc update_neighbours.perspective{direction:"west"} if score #place.update.connect microbuilder.calc matches 1 run scoreboard players remove #place.updated_state microbuilder.calc 1

execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{east:"true"} if data storage microbuilder:calc update_neighbours.perspective{direction:"east"} if score #place.update.connect microbuilder.calc matches 0 run scoreboard players add #place.updated_state microbuilder.calc 8
execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{north:"true"} if data storage microbuilder:calc update_neighbours.perspective{direction:"north"} if score #place.update.connect microbuilder.calc matches 0 run scoreboard players add #place.updated_state microbuilder.calc 4
execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{south:"true"} if data storage microbuilder:calc update_neighbours.perspective{direction:"south"} if score #place.update.connect microbuilder.calc matches 0 run scoreboard players add #place.updated_state microbuilder.calc 2
execute if data storage microbuilder:calc update_neighbours.perspective.this.state.Properties{west:"true"} if data storage microbuilder:calc update_neighbours.perspective{direction:"west"} if score #place.update.connect microbuilder.calc matches 0 run scoreboard players add #place.updated_state microbuilder.calc 1